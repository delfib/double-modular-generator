-- =========================================================
-- Queue module
-- =========================================================

MODULE Queue(Q_SIZE, client_toggle, server_toggle)
VAR
    head : 0..3;
    tail : 0..3;
    last_client_toggle : boolean;  -- last toggle value we saw
    last_server_toggle : boolean;

ASSIGN
    init(head) := 0;
    init(tail) := 0;
    init(last_client_toggle) := FALSE;
    init(last_server_toggle) := FALSE;

    next(tail) := case
        (client_toggle != last_client_toggle) : (tail + 1) mod Q_SIZE;    -- produce if toggle changed (new request detected)
        TRUE : tail;
    esac;

    next(head) := case
        (server_toggle != last_server_toggle) : (head + 1) mod Q_SIZE;
        TRUE : head;
    esac;

    next(last_client_toggle) := case
        (client_toggle != last_client_toggle) : client_toggle;
        TRUE : last_client_toggle;
    esac;

    next(last_server_toggle) := case
        (server_toggle != last_server_toggle) : server_toggle;
        TRUE : last_server_toggle;
    esac;

DEFINE
    full := ((tail + 1) mod Q_SIZE = head);
    empty := (tail = head);

FAIRNESS
    running


-- =========================================================
-- Client module
-- =========================================================
MODULE Client(queue)
VAR
    client_state : {sending, sent};
    request_toggle : boolean;           

ASSIGN
    init(client_state) := sending;
    init(request_toggle) := FALSE;

    next(client_state) :=
        case
            client_state = sending & queue.full : sending;
            client_state = sending & !queue.full : sent;
            client_state = sent & !queue.full & request_toggle = queue.last_client_toggle : sending;
            client_state = sent & queue.full : sent;
            TRUE : client_state;
        esac;
 
    -- Toggle the bit each time we make a new request
    next(request_toggle) := case
        client_state = sending & !queue.full : !request_toggle;                     -- toggle!
        TRUE : request_toggle;                                                      -- keep same value otherwise
    esac;

FAIRNESS
    running


-- =========================================================
-- Server module
-- =========================================================
MODULE Server(queue)
VAR
    server_state : {receiving, received};
    request_toggle : boolean;  
ASSIGN
    init(server_state) := receiving;
    init(request_toggle) := FALSE;

    next(server_state) :=
        case
            server_state = receiving & queue.empty : receiving;
            server_state = receiving & !queue.empty : received;
            server_state = received & !queue.empty & request_toggle = queue.last_server_toggle: receiving;
            server_state = received & queue.empty : received;
            TRUE : server_state;
        esac;

    next(request_toggle) := case
        server_state = receiving & !queue.empty : !request_toggle;                   
        TRUE : request_toggle;                                                    
    esac;

FAIRNESS
    running


-- =========================================================
-- Nominal wrapper
-- =========================================================
MODULE NominalR()
DEFINE
    Q_SIZE := 4;
VAR
    client : process Client(queue);
    server : process Server(queue);
    queue : process Queue(Q_SIZE, client.request_toggle, server.request_toggle);


-- =========================================================
-- Main module
-- =========================================================
MODULE main
VAR
    nominal  : NominalR();